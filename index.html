<html>
  <head>
    <title>ontouchstart</title>
  </head>
  <body>
<script type="text/coffeescript">
do ->
  styleTag = document.createElement 'style'
  document.head.appendChild styleTag
  document.styleSheets[0].insertRule '''
        * {
        margin: 0;
        padding: 0;
        text-decoration: none;
        -webkit-touch-callout:none;
        -webkit-user-select:none;
        -webkit-text-size-adjust:none;
        -webkit-tap-highlight-color: transparent;
      }
'''
  document.styleSheets[0].insertRule '''
      pre {
        font-family: monospace;
        font-size: 14px;
        margin: 3.5em;
        margin-top: 0;
        padding: 1em;
        border: 1px solid black;
      }
'''
  document.styleSheets[0].insertRule '''
      textarea {
        font-family: monospace;
        font-size: 14px;
        font-weight: bold;
        margin-left: 3.5em;
        margin-bottom: 3.5em;
        padding: 0.5em;
        border-radius: 0;
        color: #00FF00; background: #000000;
        -webkit-user-select: text;
      }
'''

  document.styleSheets[0].insertRule '''
      .output {
        margin: 1em; padding : 1em;
        font: 20px "American Typewriter";
        border-radius: 0.5em;
        color: #00FF00; background: #000000;
      }
'''

do ->
  licenseDiv = document.createElement 'div'
  licenseDiv.id = 'license'
  licenseDiv.innerHTML = '''
      <a href="?source">CoffeeScript code</a> by <a xmlns:cc="http://creativecommons.org/ns#" href="https://github.com/ontouchstart/ontouchstart.github.com" property="cc:attributionName" rel="cc:attributionURL">Sam Liu</a> is licensed under a <a rel="license" href="http://creativecommons.org/licenses/by/3.0/">Creative Commons Attribution 3.0 Unported License</a>.
'''

  document.styleSheets[0].insertRule '''
      #license {
        margin: 0;
        margin-bottom: 1em;
        padding: 1em;
        font-family: Helvetica;
        border-bottom: 2px solid #123;
      }
'''
  document.body.appendChild licenseDiv

do ->
  window.cDiv = document.createElement 'div'
  window.cDiv.style.position = 'relative'
  window.cDiv.style.with = '100%'
  window.cDiv.style.height = '100%'
  window.cDiv.style.margin = 'auto'
  document.body.appendChild window.cDiv

window.cs = (code) ->
  codeKey = document.location.host + document.location.pathname + 'code'
  codeValue = code
  try 
    codeValue = localStorage.getItem codeKey
    codeValue = code if codeValue.length < 1
  catch err
    codeValue = code

  localStorage.setItem codeKey, codeValue

  if document.location.search is '?source'
    reset = document.createElement 'pre'
    reset.innerHTML = '[Touch to reset CoffeeScript to default]'
    cDiv.appendChild reset

    input = document.createElement 'textarea'
    input.style.style = 'block'
    cDiv.appendChild input

    jsBlock = document.createElement 'pre'
    jsBlock.innerText = '[Touch to compile CoffeeScript]'
    cDiv.appendChild jsBlock
 
    updateInput = -> 
      lines = codeValue.split '\n'
      loc = lines.length
      input.rows = loc
      input.cols = 80
      input.value = codeValue
      input.onkeypress = (e)->        
        @rows++ if e.keyCode is 13

    reset.ontouchstart = ->
      codeValue = code
      do updateInput

    jsBlock.ontouchstart = (e) ->
      localStorage.setItem codeKey, input.value
      jsBlock.innerText = "/* Touch here to run JavaScript */ \n"
      jsBlock.innerText += CoffeeScript.compile input.value
      jsBlock.ontouchstart = -> 
        do input.blur
        input.style.display = 'none'
        reset.style.display = 'none'
        jsBlock.style.display = 'none'
        js = document.createElement 'script'
        js.innerText = jsBlock.innerText
        document.body.appendChild js
    do updateInput
  else
    js = document.createElement 'script'
    js.innerHTML = CoffeeScript.compile codeValue
    document.body.appendChild js
</script>

<script type="text/coffeescript">
cs '''
print = (message) ->
  output = document.createElement 'div'
  output.className = 'output'
  output.innerHTML = message
  cDiv.appendChild output
  return output

print "Hello, world"

setCanvas = (w = 640, h = 640) ->
  c = document.createElement 'canvas'
  c.width = w
  c.height = h
  c.style.width = "#{w}px"
  c.style.height = "#{h}px"
  c.style.margin = '1em'
  c.style.marginLeft = "#{(document.body.scrollWidth - w) / 2}px"
  cDiv.appendChild c

  ctx = c.getContext '2d'

  do ctx.beginPath
  ctx.moveTo 0,0
  ctx.lineTo w, 0
  ctx.lineTo w, h
  ctx.lineTo 0, h
  ctx.lineTo 0, 0
  ctx.fillStyle = "#abc"
  do ctx.fill
  do ctx.save

  ctx.font = "12px American Typewriter"
  ctx.fillStyle = "#123"
  ctx.fillText "ontouchstart.github.com", w - 150, h - 10
  document.title = "Touch to draw within the box"

  c.ontouchstart = (e) ->
    if e.touches.length > 1
      document.location = do c.toDataURL
      return
    else
      do ctx.restore
      do ctx.beginPath
      x = e.touches[0].pageX - c.offsetLeft - c.parentNode.offsetLeft
      y = e.touches[0].pageY - c.offsetTop - c.parentNode.offsetTop
      ctx.moveTo x, y

  c.ontouchmove = (e) ->
    do e.preventDefault
    x = e.touches[0].pageX - c.offsetLeft - c.parentNode.offsetLeft
    y = e.touches[0].pageY - c.offsetTop - c.parentNode.offsetTop
    ctx.lineTo x, y
    do ctx.stroke

  c.ontouchend = (e) ->
    document.title = "Two finger touch to save"

    do ctx.save

  return ctx

ctx = setCanvas 640, 480
ctx.font = "44px American Typewriter"
ctx.fillStyle = "#321"
ctx.fillText "Hello, world", 10, 40

inspect = ->
  for k,v of @
    print k
    print v

inspect.apply navigator
inspect.apply document
'''
</script>
<script type="text/javascript" src="coffee-script-1.2.js"></script>
</body>
</html>

