<html>
  <head>
    <title>ontouchstart</title>
  </head>
  <body>
<script type="text/javascript">
window.touchUI = ('ontouchstart' in window)? true : false;
</script>
<script type="text/coffeescript">
license = document.createElement 'div'
license.style.margin = '0'
license.style.marginBottom = '1em'
license.style.padding = '1em'
license.style.borderBottom = '2px solid #123'
license.style.fontFamily = 'Helvetica'

license.innerHTML = '''
<a href="?source">CoffeeScript code</a> 
by <a xmlns:cc="http://creativecommons.org/ns#" href="https://github.com/ontouchstart/ontouchstart.github.com" property="cc:attributionName" rel="cc:attributionURL">Sam Liu</a>
is licensed under a 
<a rel="license" href="http://creativecommons.org/licenses/by/3.0/">Creative Commons Attribution 3.0 Unported License</a>.
'''

styleTag = document.createElement 'style'
document.head.appendChild styleTag
ss = document.styleSheets[0]

ss.insertRule '''
* {
  margin: 0;
  padding: 0;
  font-size: 18px;
  text-decoration: none;
  -webkit-touch-callout:none;
  -webkit-user-select:none;
  -webkit-text-size-adjust:none;
  -webkit-tap-highlight-color: transparent;
}
''', ss.cssRules.length

ss.insertRule '''
pre {
  font-family: monospace;
  font-size: 14px;
  margin: 3.5em;
  margin-top: 0;
  padding: 1em;
  border: 1px solid black;
 }
''', ss.cssRules.length

ss.insertRule '''
textarea {
  font-family: monospace;
  font-size: 14px;
  font-weight: bold;
  margin-left: 3.5em;
  margin-bottom: 3.5em;
  padding: 0.5em;
  border-radius: 0;
  color: #00FF00; background: #000000;
  -webkit-user-select: text;
}
''', ss.cssRules.length

window.cs = (code) ->
  codeKey = document.location.host + document.location.pathname + 'code'
  codeValue = code
  try 
    codeValue = localStorage.getItem codeKey
    codeValue = code if codeValue.length < 1
  catch err
    codeValue = code

  localStorage.setItem codeKey, codeValue

  if document.location.search is '?source'
    container = document.createElement 'div'
    container.style.position = 'relative'
    container.style.width = '100%'
    container.style.height = '100%'
    container.style.margin = 'auto'

    input = document.createElement 'textarea'
    input.style.style = 'block'
    container.appendChild input

    resetButton = document.createElement 'pre'
    resetButton.innerHTML = '[Reset CoffeeScript]'
    container.appendChild resetButton

    jsBlock = document.createElement 'pre'
    jsBlock.innerHTML = '[Compile CoffeeScript]'
    container.appendChild jsBlock
 
    updateInput = ->
      lines = codeValue.split '\n'
      loc = lines.length
      input.rows = loc
      input.cols = 100
      input.value = codeValue
      input.onkeypress = (e)->
        @rows++ if e.keyCode is 13

    reset = ->
      codeValue = code
      do updateInput

    if touchUI
      resetButton.ontouchstart = reset
    else
      resetButton.onclick = reset

    compile = ->
      localStorage.setItem codeKey, input.value
      jsBlock.innerHTML = "/* Run JavaScript */ \n" # for Firefox
      jsBlock.innerHTML += CoffeeScript.compile input.value # for Firefox

      jsBlock.innerText = "/* Run JavaScript */ \n" # for code 
      jsBlock.innerText += CoffeeScript.compile input.value # for code

      run = ->
        do input.blur
        container.style.display = 'none'
        eval jsBlock.innerText

      if touchUI
        jsBlock.ontouchstart = run
      else
        jsBlock.onclick = run
        
    if touchUI
      jsBlock.ontouchstart = compile
    else
      jsBlock.onclick = compile

    do updateInput
    document.body.appendChild container

  else
    eval CoffeeScript.compile codeValue

document.body.appendChild license
</script>

<script type="text/coffeescript">
cs '''
w = 640
h = 480
container = document.createElement 'div'
container.style.position = 'relative'
container.style.width = "#{w}px"
container.style.height = "#{h}px"
container.style.margin = 'auto'
container.style.marginTop = '100px'
container.style.marginBottom = '100px'

ns = 'http://www.w3.org/2000/svg'

svgTrail = document.createElementNS ns, 'svg'
svgTrail.width = w
svgTrail.height = h
svgTrail.style.width = "#{w}px"
svgTrail.style.height = "#{h}px"
svgTrail.style.position = 'absolute'
container.appendChild(svgTrail)

bezierCurve = (name, x0, y0, x1, y1, x2, y2, x3, y3) ->
  rule = '@-webkit-keyframes ' + name  + ' {'
  for p in [0..100]
    t = p / 100
    a = (1-t) * (1-t) * (1-t)
    b = 3 * (1-t) * (1-t) * t
    c = 3 *(1-t) * t * t
    d = t * t * t
    x = a * x0 + b * x1 + c * x2 + d * x3
    y = a * y0 + b * y1 + c * y2 + d * y3
    rule += "#{p}%{"
    rule += "-webkit-transform:translate3d(#{x}px,#{y}px,0)"
    rule += "rotate(#{t*180}deg)}"       
  rule += '}'
  document.styleSheets[0].insertRule rule

  svgTrailPath = document.createElementNS ns, 'path'
  svgTrailPath.setAttribute 'stroke', 'green'
  svgTrailPath.setAttribute 'fill', 'none'
  svgTrailPath.setAttribute 'd', "M#{x0},#{y0}C#{x1},#{y1},#{x2},#{y2},#{x3},#{y3}"
  svgTrail.appendChild svgTrailPath
   
svgBezierTrail = ->
  svg = document.createElementNS ns, 'svg'
  svg.width = w/10
  svg.height = h/10
  svg.style.width = "#{w/10}px"
  svg.style.height = "#{h/10}px"
  svg.style.border = 'none'
  container.appendChild svg

  path = document.createElementNS ns, 'path'
  path.setAttribute 'fill', 'green'
  path.setAttribute 'd', "M0,0C#{w/10},0,0,#{h/10},#{w/10},#{h/10}C#{w/10},0,0,#{h/10},0,0z"
  svg.appendChild path
  svg.style.position = 'absolute'
  svg.style.left = "#{-w/20}px"
  svg.style.top = "#{-h/20}px"

  bezierCurve 'forward', 0, 0, w, 0, 0, h, w, h
  bezierCurve 'backward', w, h, w, 0, 0, h, 0, 0
 
  svg.style.webkitAnimationIterationCount = 1
  svg.style.webkitAnimationDuration = '4000ms'
  svg.style.webkitAnimationTimingFunction = 'linear'
  svg.style.webkitAnimationFillMode = 'both'

  forward = -> 
    svg.removeEventListener 'webkitAnimationEnd', forward
    svg.addEventListener 'webkitAnimationEnd', backward
    svg.style.webkitAnimationName = 'forward'

  backward = ->
    svg.removeEventListener 'webkitAnimationEnd', backward
    svg.addEventListener 'webkitAnimationEnd', forward
    svg.style.webkitAnimationName = 'backward'

  do forward

do svgBezierTrail
document.body.appendChild container
'''
</script>
<script type="text/javascript" src="../coffee-script-1.2.js"></script>
</body>
</html>

